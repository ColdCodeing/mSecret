buildscript {
    ext {
        kotlin_version = '1.2.60'
        vertx_version = '3.5.3'
    }

    repositories {
        jcenter()
//    maven {
//      url "https://oss.sonatype.org/content/repositories/snapshots/"
//    }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group 'com.mm'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin'

sourceCompatibility = '1.8'

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.mm.MainVerticle'

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/3963/"
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin-coroutines:$vertx_version"
    compile "io.vertx:vertx-lang-kotlin:$vertx_version"
    compile "io.vertx:vertx-mysql-postgresql-client:$vertx_version"
    compile "io.vertx:vertx-mail-client:$vertx_version"
    compile "io.vertx:vertx-auth-jwt:$vertx_version"
    compile "org.slf4j:slf4j-api:1.8.0-beta2"

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
}

// Redeploy watcher.
run {
    args = ['run', mainVerticleName,
            "--launcher-class=$mainClassName",
            "--redeploy=src/**/*.*",
            "--on-redeploy=./gradlew classes"
    ]
}

// Naming and packaging settings for the "shadow jar".
shadowJar {
    baseName = 'secret'
    classifier = 'shadow'

    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

// Heroku relies on the 'stage' task to deploy.
task stage {
    dependsOn shadowJar
}
kotlin {
    experimental {
        coroutines "enable"
    }
}